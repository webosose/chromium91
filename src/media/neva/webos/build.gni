# Copyright 2018-2020 LG Electronics, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# SPDX-License-Identifier: Apache-2.0

import("//neva/neva.gni")

media_player_neva_webos = {
  configs = []
  defines = []
  public_configs = []
  deps = []
  public_deps = [ "//base" ]
  defines = []
  _dir = get_path_info("../webos", "abspath")
  sources = [
    "$_dir/media_player_neva_factory_webos.cc",
    "$_dir/media_player_ums.cc",
    "$_dir/media_player_ums.h",
    "$_dir/webos_mediaclient.h",
  ]

  use_umediaclient = false
  use_luna_service = false

  if (use_gst_media) {
    configs += [ "//media/neva/webos:media_base_gst_config" ]
    defines += [ "UMS_INTERNAL_API_VERSION=2" ]
    deps += [ "//base/neva/webos" ]

    use_umediaclient = true
  }

  if (use_umediaclient) {
    sources += [
      "$_dir/media_util.cc",
      "$_dir/media_util.h",
      "$_dir/umediaclient_extension.cc",
      "$_dir/umediaclient_extension.h",
      "$_dir/umedia_info_util_webos.cc",
      "$_dir/umedia_info_util_webos.h",
      "$_dir/umediaclient_impl.cc",
      "$_dir/umediaclient_impl.h",
    ]

    if (use_gst_media) {
      sources -= [
        "$_dir/umediaclient_extension.cc",
      ]
      sources += [
        "$_dir/umediaclient_extension_stub.cc",
        "$_dir/umediaclient_extension_stub.h",
        "$_dir/umedia_info_util_webos_gmp.cc",
        "$_dir/umedia_info_util_webos_gmp.h",
      ]
    }
  }

  if (use_neva_media_player_camera) {
    defines += [ "USE_NEVA_MEDIA_PLAYER_CAMERA=1" ]
    sources += [
      "$_dir/media_player_camera.cc",
      "$_dir/media_player_camera.h",
    ]
  }

  if (use_luna_service) {
    deps += [ "//base/neva/webos" ]
  }
}

media_platform_api_webos = {
  configs = []
  public_configs = []
  deps = []
  public_deps = [ "//base" ]
  _dir = get_path_info("../webos", "abspath")
  sources = [ "$_dir/media_preferences_webos.cc" ]

  if (use_gst_media) {
    configs += [ "//media/neva/webos:media_base_gst_config" ]
    sources += [
      "$_dir/media_platform_api_webos_gmp.cc",
      "$_dir/media_platform_api_webos_gmp.h",
    ]
  } else {
    sources += [
      "$_dir/media_platform_api_webos_stub.cc",
      "$_dir/media_platform_api_webos_stub.h",
    ]
  }
}
